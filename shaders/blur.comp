#version 450 core

layout(local_size_x = 16, local_size_y = 16) in;

// Input: binding=0, readonly
// Input: binding=0, readonly
#ifdef FORMAT_R8
layout(r8, binding=0) uniform readonly image2D inImage;
layout(r8, binding=1) uniform writeonly image2D outImage;
#elif defined(FORMAT_RG8)
layout(rg8, binding=0) uniform readonly image2D inImage;
layout(rg8, binding=1) uniform writeonly image2D outImage;
#else
layout(rgba8, binding=0) uniform readonly image2D inImage;
layout(rgba8, binding=1) uniform writeonly image2D outImage;
#endif

// Dimensione
uniform ivec2 uImageSize;

// (Opzionale) fade
uniform float uFade;
uniform float uToneExposure; // >0, controls log compression
uniform float uAutoDimThreshold; // 0..1
uniform float uAutoDimStrength;  // 0..1
uniform float uAutoDimGlobal;    // global compression factor

void main()
{
    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    if (gid.x>=uImageSize.x || gid.y>=uImageSize.y) return;

    // Box blur 3x3
    vec4 sum=vec4(0.0);
    for (int j=-1; j<=1; j++){
        for (int i=-1; i<=1; i++){
            ivec2 coord=gid+ivec2(i,j);
            coord=clamp(coord, ivec2(0), uImageSize-ivec2(1,1));
            sum += imageLoad(inImage, coord);
        }
    }
    vec4 blurred = sum/9.0;

    // fade
    // se uFade<1.0, riduciamo intensità
    blurred *= uFade;

    // Log-like tone mapping to compress highlights
    float k = max(uToneExposure, 0.001);
    vec3 toned = log(1.0 + blurred.rgb * k) / log(1.0 + k);
    // Adaptive dimming: if luminanza locale è sopra soglia, sottraiamo di più
#if defined(FORMAT_R8) || defined(FORMAT_RG8)
    float lum = toned.r;
#else
    float lum = dot(toned, vec3(0.299, 0.587, 0.114));
#endif
    float over = max(0.0, lum - uAutoDimThreshold) / max(1.0 - uAutoDimThreshold, 1e-4);
    float dim = 1.0 - uAutoDimStrength * over;
    // Global compression: più luminanza totale -> più attenuazione
    float globalDim = 1.0 / (1.0 + uAutoDimGlobal * lum);
    blurred = vec4(toned * dim * globalDim, blurred.a);

    imageStore(outImage, gid, blurred);
}
